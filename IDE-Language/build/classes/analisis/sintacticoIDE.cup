package analisis;

import java_cup.runtime.*;
import analisis.semantic.*;
import analisis.semanticObjects.*;
import languageConstants.languageConstants;
import exceptions.InputsVaciosException;

parser code {:

	private semanticManager semanticM;
	private operation ops;
	languageConstants languageC = new languageConstants();

	public sintactico(Lexer lex, semanticManager semanticM, operation ops){
		super(lex);
		this.semanticM = semanticM;
		this.ops = ops;
	}

    @Override
	public void syntax_error(Symbol cur_token) {
		String message = "Estructura invalida en Columna: " + cur_token.right
			 + " Fila: " + cur_token.left + " , token: " + cur_token.value;
		System.out.println(">>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<");	 
		System.out.println(message);
		System.out.println("<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<");
		semanticM.reset3DirCode();
		ops.resetTemp3Dir();
	}
:}

/* Terminal */
terminal String NamInt, NamFloat, NamBoolean, NamString, NamMain, NamIf, NamWhile;
terminal String PrintN, PrintS, BreakCom, Mas, Menos, Por, Div, Mayor, Menor, Igual, And;
terminal String Or, Asignacion, EndCom, ParAbierto, ParCerrado, Coma, Booleano, Cadena;
terminal String Entero, Decimal, Identificador, NamElse, Diferente, CorCerrado, CorAbierto;

/* Non terminal */
non terminal String CODIGO, INICIO_CODIGO;
non terminal Boolean IF_ELSE, MAIN_IDE, MAIN_BODY, MAIN_FUNCION, CUERPO_CODIGO;
non terminal Boolean IF_ELSE_FUNCION, INSIDE_CODE, WHILE_FUNCION, PRINT_FUNCION, PRINT_OPTION;
non terminal noDefine OPERACION, CONCATENAR;
non terminal variableObject ID_VAR, ID_STRING;
non terminal Integer DECLARAR_STRING, DECLARACION_VAR; 
/*Precedence*/
precedence left ParAbierto, ParCerrado;
precedence left Mas, Menos;
precedence left Por, Div;
precedence left Menor, Mayor;
precedence left Igual, Diferente;
precedence left And, Or;

//start with DECLARACION_VAR;

start with INICIO_CODIGO;

INICIO_CODIGO	::= CODIGO {: semanticM.create3DirCodeDoc(); :}
					;

CODIGO 			::=	CUERPO_CODIGO CODIGO					{: :}
					| MAIN_IDE:e CorCerrado					{: semanticM.breakOutOfWhile(e); :}
					;

CUERPO_CODIGO	::=	DECLARACION_VAR EndCom		{: :}
					| DECLARAR_STRING EndCom	{: :}
					;

MAIN_IDE		::=	NamMain ParAbierto ParCerrado CorAbierto MAIN_BODY:e	{: RESULT = semanticM.findBreak(e, false); :}
					;

MAIN_BODY		::=	MAIN_BODY MAIN_FUNCION:e	{: RESULT = semanticM.findBreak(e, false); :}
					| MAIN_FUNCION:e			{: RESULT = semanticM.findBreak(e, false); :}
					;

MAIN_FUNCION	::=	IF_ELSE_FUNCION:e				{: RESULT = semanticM.findBreak(e, false); :}
					| PRINT_FUNCION:e				{: RESULT = semanticM.findBreak(e, false); :}
					| WHILE_FUNCION:e				{: RESULT = semanticM.findBreak(e, false); :}
					| DECLARACION_VAR EndCom		{: RESULT = false; :}
					| DECLARAR_STRING EndCom		{: RESULT = false; :}
					| ID_VAR:e1 EndCom:e2			{: semanticM.modifyValue(e1, e2left);  RESULT = false; :}
					| ID_STRING:e1 EndCom:e2		{: semanticM.modifyValue(e1, e2left);  RESULT = false; :}
					| BreakCom EndCom				{: RESULT = true; :}
					;

DECLARACION_VAR	::=	DECLARACION_VAR:e1 Coma:e3 ID_VAR:e2		{: semanticM.addVariableToList(e1, e2, e3left, e3right); RESULT = e1; :}
					| NamInt:e3 ID_VAR:e1						{: semanticM.addVariableToList(languageC.INTEGER, e1, e3left, e3right); RESULT = languageC.INTEGER; :}
					| NamBoolean:e3 ID_VAR:e1					{: semanticM.addVariableToList(languageC.BOOLEAN, e1, e3left, e3right); RESULT = languageC.BOOLEAN; :}
					| NamFloat:e3 ID_VAR:e1						{: semanticM.addVariableToList(languageC.FLOAT, e1, e3left, e3right); RESULT = languageC.FLOAT; :}
					;

ID_VAR			::=	Identificador:e								{: RESULT = ops.tempVar(e, null); :}
					| Identificador:e1 Asignacion OPERACION:e2 	{: RESULT = ops.tempVar(e1, e2); :}
					;

OPERACION		::=	OPERACION:e1 Mas:e3 OPERACION:e2 			{: RESULT = ops.arithmeticOp(languageC.ADD,	e1, e2, e3left, e3right); :}
					| OPERACION:e1 Menos:e3 OPERACION:e2 		{: RESULT = ops.arithmeticOp(languageC.LESS,e1, e2, e3left, e3right); :}
					| OPERACION:e1 Por:e3 OPERACION:e2 			{: RESULT = ops.arithmeticOp(languageC.MUL,	e1, e2, e3left, e3right); :}
					| OPERACION:e1 Div:e3 OPERACION:e2 			{: RESULT = ops.arithmeticOp(languageC.DIV,	e1, e2, e3left, e3right); :}
					| OPERACION:e1 Menor:e3 OPERACION:e2 		{: RESULT = ops.logicOp(languageC.MENOR_Q, 	e1, e2, e3left, e3right); :}
					| OPERACION:e1 Mayor:e3 OPERACION:e2 		{: RESULT = ops.logicOp(languageC.MAYOR_Q, 	e1, e2, e3left, e3right); :}
					| OPERACION:e1 Igual:e3 OPERACION:e2 		{: RESULT = ops.logicOp(languageC.IGUAL, 	e1, e2, e3left, e3right); :}
					| OPERACION:e1 Diferente:e3 OPERACION:e2 	{: RESULT = ops.logicOp(languageC.DIFERENTE, e1,e2, e3left, e3right); :}
					| OPERACION:e1 Or:e3 OPERACION:e2 			{: RESULT = ops.logicOp(languageC.OR, 		e1, e2, e3left, e3right); :}
					| OPERACION:e1 And:e3 OPERACION:e2			{: RESULT = ops.logicOp(languageC.AND, 		e1, e2, e3left, e3right); :}
					| ParAbierto OPERACION:e ParCerrado			{: RESULT = e; :}
					| Entero:e									{: RESULT =  new noDefine(Double.parseDouble(e), languageC.DOUBLE_AUX, eleft, eright); :}
					| Decimal:e									{: RESULT =  new noDefine(Double.parseDouble(e), languageC.DOUBLE_AUX, eleft, eright); :}
					| Identificador:e 							{: RESULT = semanticM.returnNoDefineObject(e, eleft, eright); :}
					| Booleano:e 								{: RESULT = new noDefine(Boolean.valueOf(e), languageC.BOOL_AUX, eleft, eright); :}
					;

DECLARAR_STRING	::=	DECLARAR_STRING:e1 Coma:e3 ID_STRING:e2		{: semanticM.addVariableToList(e1, e2, e3left, e3right); RESULT = e1; :}
						| NamString:e2 ID_STRING:e1				{: semanticM.addVariableToList(languageC.STRING, e1, e2left, e2right); RESULT = languageC.STRING; :}
						;

ID_STRING		::=	Identificador:e1 Asignacion CONCATENAR:e2	{: RESULT = ops.tempVar(e1, e2); :}
					;

CONCATENAR		::= CONCATENAR:e1 Mas:e3 Cadena:e2				{: RESULT = ops.stringOp(e1, new noDefine(e2, languageC.STRING_AUX, e2left, e2right), e3left, e3right); :}
					| CONCATENAR:e1 Mas:e3 OPERACION:e2 		{: RESULT = ops.stringOp(e1, e2, e3left, e3right); :}
					| OPERACION:e1 Mas:e3 CONCATENAR:e2 		{: RESULT = ops.stringOp(e1, e2, e3left, e3right); :}
					| ParAbierto CONCATENAR:e ParCerrado		{: RESULT = e; :}
					| Cadena:e	 								{: RESULT = new noDefine(e, languageC.STRING_AUX, eleft, eright); :}
					;

IF_ELSE_FUNCION	::= IF_ELSE:e 									{: RESULT = semanticM.findBreak(e, false); :}
					| IF_ELSE:e1 NamElse INSIDE_CODE:e2			{: RESULT = semanticM.findBreak(e1, e2); :}
					;

IF_ELSE 		::= IF_ELSE:e1 NamElse NamIf ParAbierto OPERACION:e2 ParCerrado INSIDE_CODE:e3 	{: semanticM.correctIfOperator(e2); RESULT = semanticM.findBreak(e1, e3); :}
					| NamIf ParAbierto OPERACION:e1 ParCerrado INSIDE_CODE:e2					{: semanticM.correctIfOperator(e1); RESULT = semanticM.findBreak(e2	, false); :}
					;

INSIDE_CODE		::= CorAbierto MAIN_BODY:e CorCerrado			{: RESULT = semanticM.findBreak(e, false); :}
					;

WHILE_FUNCION	::= NamWhile ParAbierto OPERACION:e1 ParCerrado INSIDE_CODE:e2 	{: semanticM.correctIfOperator(e1); RESULT = semanticM.findBreakInWhile(e2); :}
					;

PRINT_FUNCION	::= PRINT_OPTION ParAbierto CONCATENAR:e ParCerrado EndCom		{: semanticM.correctStringOperator(e); RESULT = false;:}
					;

PRINT_OPTION	::= PrintN 		{: :}
					| PrintS	{: :}
					;
